{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c51d2214-54cb-4abf-b03f-90d72e630e91",
   "metadata": {},
   "source": [
    "# Weierstrass normal form\n",
    "\n",
    "The goal is to bring a curve over the rationals into its corresponding\n",
    "Weierstrass normal form. We are given the general homogeneous polynomial\n",
    "$$F(X, Y, Z) = 8X^3+40X^2Y+75XY^2+46Y^3+53X^2Z+218XYZ+197Y^2Z+168XZ^2+276YZ^2+118Z^3$$ of degree $3$. This polynomial defines a curve $C = \\{F(X, Y, Z) = 0\\}.$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "29f71094-abf2-4801-8d71-ff5565756b5e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  ___   ____   ____    _    ____\n",
      " / _ \\ / ___| / ___|  / \\  |  _ \\   |  Combining ANTIC, GAP, Polymake, Singular\n",
      "| | | |\\___ \\| |     / _ \\ | |_) |  |  Type \"?Oscar\" for more information\n",
      "| |_| | ___) | |___ / ___ \\|  _ <   |  Manual: https://docs.oscar-system.org\n",
      " \\___/ |____/ \\____/_/   \\_\\_| \\_\\  |  Version 1.3.1\n"
     ]
    }
   ],
   "source": [
    "using Oscar"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "b9bbacc6-ddcd-4325-97a5-610fc8080582",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(Multivariate polynomial ring in 3 variables over QQ, QQMPolyRingElem[X, Y, Z])"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Q3, (X, Y, Z) = polynomial_ring(QQ, [:X, :Y, :Z])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "07c25306-d293-4683-a5ee-3b3024681b94",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "8*X^3 + 40*X^2*Y + 53*X^2*Z + 75*X*Y^2 + 218*X*Y*Z + 168*X*Z^2 + 46*Y^3 + 197*Y^2*Z + 276*Y*Z^2 + 118*Z^3"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "F = 8*X^3+40*X^2*Y+75*X*Y^2+46*Y^3+53*X^2*Z+218*X*Y*Z+197*Y^2*Z+168*X*Z^2+276*Y*Z^2+118*Z^3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "042596b6-fb13-435a-a85b-0a9a55894ae4",
   "metadata": {},
   "source": [
    "# Smoothness\n",
    "First, we want to determine if $C$ is a smooth curve. Therefore, we need to find the vanishing locus of the homogeneous gradient $\\nabla F$ of our polynomial \\(F\\). To achieve this, we have to find the common zeros of the partial derivatives of $F$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f45517e3-0804-42a7-bfe4-6593725b14eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "dF_dx = derivative(F, X) \n",
    "dF_dy = derivative(F, Y)\n",
    "dF_dz = derivative(F, Z)\n",
    "\n",
    "DF = matrix(Q3, 1, 3, [dF_dx, dF_dy, dF_dz])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "54234fdb-5bfd-4b6b-bcd4-760158ac98f0",
   "metadata": {},
   "source": [
    "Now we look at the ideal generated by the partial derivatives $$I = (\\frac{dF}{dX}, \\frac{dF}{dY}, \\frac{dF}{dZ}).$$  We decompose it into its primary\n",
    "ideals and check, where they vanish."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a41f10ca-1c8b-4dc0-9744-8991c4bbb69d",
   "metadata": {},
   "outputs": [],
   "source": [
    "I1 = ideal(Q3, [dF_dx, dF_dy, dF_dz]) \n",
    "saturation(I1)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f58b49a-0107-4427-8926-6696780316fb",
   "metadata": {},
   "source": [
    "Since the saturated ideal is generated by $1$, the gradient $\\nabla F$ only vanishes in $[0:0:0] \\notin \\mathbb{P}^2(\\mathbb{Q}).$ We conclude that $C$ is smooth."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e1a39585-bece-4c64-8cfe-dd4618886a40",
   "metadata": {},
   "source": [
    "# Flex point\n",
    "\n",
    "To find the corresponding Weierstrass normal form, we need to find a flex point of $C$. So we calculate the second partial derivatives, the Hessian of $F$ and proceed as with the vanishing locus of the $\\nabla F$ to find common zeros of $F$ and $det(H_{F})$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f4ad8a60-4886-41c0-a4d5-844e819ea679",
   "metadata": {},
   "outputs": [],
   "source": [
    "d2F_dxdx = derivative(dF_dx, X) \n",
    "d2F_dxdy = derivative(dF_dx, Y)\n",
    "d2F_dxdz = derivative(dF_dx, Z)\n",
    "d2F_dydx = d2F_dxdy #Satz von Schwartz\n",
    "d2F_dydy = derivative(dF_dy, Y)\n",
    "d2F_dydz = derivative(dF_dy, Z)\n",
    "d2F_dzdx = d2F_dxdz\n",
    "d2F_dzdy = d2F_dydz\n",
    "d2F_dzdz = derivative(dF_dz, Z)\n",
    "\n",
    "HF = matrix(Q3, 3, 3, [d2F_dxdx, d2F_dxdy, d2F_dxdz, d2F_dydx, d2F_dydy, d2F_dydz, d2F_dzdx, d2F_dzdy, d2F_dzdz]) \n",
    "I2 = ideal(Q3, [F, det(HF)]) \n",
    "decomp2 = primary_decomposition(I2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6ece6154-1ed9-4804-b983-aeaf8b9f7432",
   "metadata": {},
   "source": [
    "The ideal reduced to $I = (2Y+5Z, X - 2Z)$.\n",
    "\n",
    "Now we need to find $(X, Y, Z)$, such that \n",
    "$$\\begin{array} 2Y+5Z = 0 \\\\ X - 2Z = 0. \\end{array}$$ \n",
    "\n",
    "Thus, we solve for the kernel of the matrix $M = \\begin{pmatrix} 0 & 2 & 5 \\\\ 1 & 0 & -2\\end{pmatrix}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "333aeae3-d01f-4ada-a70a-1112d65ca74d",
   "metadata": {},
   "outputs": [],
   "source": [
    "M = matrix(QQ, 2, 3, [0, 2, 5, 1, 0, -2])\n",
    "P = nullspace(M)[2]\n",
    "R = 2P #rescaling for integer coordinates"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7dfe90a-a228-4717-9283-3ba502599227",
   "metadata": {},
   "source": [
    "So we found a point $R = \\begin{pmatrix} 4 \\\\ -5 \\\\ 2 \\end{pmatrix} \\in ker(M)$. Therefore $R = [4:-5:2]$ is a flex point of $C$.\n",
    "\n",
    "# Linear change of coordinates\n",
    "For the Weierstrass normal form, we have to map the tangent $L$ at $R$ to the line $\\{Z = 0\\}$.\n",
    "\n",
    "The general tangent line $L$ at a point $R = (R_{X}, R_{Y}, R_{Z})$ is given by: \n",
    "$$L : \\{ \\frac{dF}{dX}(R)(X - R_{X}) + \\frac{dF}{dY}(R)(Y - R_{Y}) + \\frac{dF}{dZ}(R)(Z - R_{Z}) = 0 \\}.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f54452b0-2765-4755-b497-033786935236",
   "metadata": {},
   "outputs": [],
   "source": [
    "L = dF_dx(R[1], R[2], R[3])*(X - R[1]) + dF_dy(R[1], R[2], R[3])*(Y - R[2]) + dF_dz(R[1], R[2], R[3])*(Z - R[3])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "158acb13-51de-43f1-8d8c-6293a0a77d77",
   "metadata": {},
   "source": [
    "Therefore $L = \\{ -X-2Y-3Z = 0\\}$ and certainly $Q = [1:1:-1] \\in L$. \n",
    "\n",
    "Now we want to map $L$ to $\\{Z=0\\}$ by mapping $Q$ to $[1:0:0]$ and our flex point $R$ to $O=[0:1:0]$, the usual point at infinity on an elliptic curve.\n",
    "\n",
    "So we obtain the matrix\n",
    "$$M_1 = \\begin{pmatrix} 1 & 4 & \\cdot \\\\ 0 & -5 & \\cdot \\\\ 0 & 2 & \\cdot \\end{pmatrix}$$\n",
    "where we can choose the last column freely, such that $M_1$ is\n",
    "invertible. In this case, $[0:0:1]$ is sufficient.\n",
    "\n",
    "So we have the new variables\n",
    "$\\begin{pmatrix} X' \\\\ Y' \\\\ Z' \\end{pmatrix} = \\begin{pmatrix} 1 & 4 & 0 \\\\ 0 & -5 & 0 \\\\ 0 & 2 & 1\\end{pmatrix} \\cdot \\begin{pmatrix} X\\\\ Y \\\\ Z \\end{pmatrix} = \\begin{pmatrix} X+4Y+Z \\\\ X-5Y \\\\ -X+2Y \\end{pmatrix}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a0344633-ee5b-4a80-98c0-d4e2c8bfe6b8",
   "metadata": {},
   "outputs": [],
   "source": [
    "M1 = matrix(QQ, 3, 3, [1, 4, 1, 1, -5, 0, -1, 2, 0]) #maps R to O = [0:1:0] and L to {Z = 0}\n",
    "v1 = M1*[X, Y, Z]\n",
    "F1 = F(v1[1], v1[2], v1[3])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "245c645e-9d1a-4825-b4f8-ab5ba8da1864",
   "metadata": {},
   "source": [
    "Our $F$ becomes\n",
    "$$F_1(X', Y', Z') = 27X'^3 + 23X'^2Z' + 4X'Y'Z' + 11X'Z'^2 - Y'^2Z' + 2Y'Z'^2 + 8Z'^3.$$\n",
    "\n",
    "We clearly see $F_1(O) = F_1(0, 1, 0) = 0$ and thus $O \\in C$.\n",
    "\n",
    "First, we rescale the coefficient of $X^3$ with the transformation matrix\n",
    " \n",
    "$$ M_2 = \\begin{pmatrix} \\frac{1}{3} & 0 & 0 \\\\ 0 & 1 & 0 \\\\ 0 & 0 & 1\\end{pmatrix}.$$\n",
    "\n",
    "So we get the new variables \n",
    "$$\\begin{pmatrix} X' \\\\ Y' \\\\ Z' \\end{pmatrix} = \\begin{pmatrix} \\frac{1}{3} & 0 & 0 \\\\ 0 & 1 & 0 \\\\ 0 & 0 & 1\\end{pmatrix} \\cdot \\begin{pmatrix} X\\\\ Y \\\\ Z \\end{pmatrix} = \\begin{pmatrix} \\frac{X}{3} \\\\ Y \\\\ Z \\end{pmatrix}.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fc84a89a-98fe-4118-85eb-8e2737304b2d",
   "metadata": {},
   "outputs": [],
   "source": [
    "M2 = matrix(QQ, 3, 3, [1//3, 0, 0, 0, 1, 0, 0, 0, 1]) #rescaling\n",
    "v2 = M2*[X, Y, Z]\n",
    "F2 = F1(v2[1], v2[2], v2[3])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "65a97b68-d59d-4394-bc28-b3548a8390dd",
   "metadata": {},
   "source": [
    "The polynomial $F_1$ becomes \n",
    "$$F_2(X', Y', Z') = X'^3 + \\frac{23}{9}X'^2Z' + \\frac{4}{3}X'Y'Z' + \\frac{11}{3}X'Z'^2 - Y'^2Z' + 2Y'Z'^2 + 8Z'^3.$$\n",
    "\n",
    "For a quadratic extension, we need\n",
    "$$M_3 = \\begin{pmatrix} 1 & 0 & 0 \\\\ \\frac{2}{3} & 1 & 1 \\\\ 0 & 0 & 1\\end{pmatrix}.$$ \n",
    "\n",
    "and obtain the new variables \n",
    "$$\\begin{pmatrix} X' \\\\ Y' \\\\ Z' \\end{pmatrix} = \\begin{pmatrix} 1 & 0 & 0 \\\\ \\frac{2}{3} & 1 & 1 \\\\ 0 & 0 & 1\\end{pmatrix} \\cdot \\begin{pmatrix} X\\\\ Y \\\\ Z \\end{pmatrix} = \\begin{pmatrix} X \\\\ Y + \\frac{2}{3}X + Z \\\\ Z \\end{pmatrix}.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4a92a53c-f0ad-454a-b914-284075c2787f",
   "metadata": {},
   "outputs": [],
   "source": [
    "M3 = matrix(QQ, 3, 3, [1, 0, 0, 2//3, 1, 1, 0, 0, 1]) #quadratic extension\n",
    "v3 = M3*[X, Y, Z]\n",
    "F3 = F2(v3[1], v3[2], v3[3])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d3640623-c846-45dd-b5a1-4966f917bbbf",
   "metadata": {},
   "source": [
    "Now $F_2$ becomes \n",
    "$$F_3(X', Y', Z') = X'^3 + 3X'^2Z' + 5X'Z'^2 - Y'^2Z' + 9Z'^3.$$\n",
    "\n",
    "All that is left to do, is cubic extension to get rid of the $X^2Z$ term. So we transform the coordinates one last time with\n",
    "$$M_4 = \\begin{pmatrix} 1 & 0 & -1 \\\\ 0 & 1 & 0 \\\\ 0 & 0 & 1\\end{pmatrix}.$$\n",
    "\n",
    "And we obtain \n",
    "$$\\begin{pmatrix} X' \\\\ Y' \\\\ Z' \\end{pmatrix} = \\begin{pmatrix} 1 & 0 & -1 \\\\ 0 & 1 & 0 \\\\ 0 & 0 & 1\\end{pmatrix} \\cdot \\begin{pmatrix} X\\\\ Y \\\\ Z \\end{pmatrix} = \\begin{pmatrix} X - Z \\\\ Y \\\\ Z \\end{pmatrix}.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e00447dd-7b7e-4bee-a2bf-3111fc1f51d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "M4 = matrix(QQ, 3, 3, [1, 0, -1, 0, 1, 0, 0, 0, 1]) #cubic extension\n",
    "v4 = M4*[X, Y, Z]\n",
    "F4 = F3(v4[1], v4[2], v4[3])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "53c56243-1515-4173-8a6a-59f2f531f31f",
   "metadata": {},
   "source": [
    "Finally $F_3$ becomes \n",
    "$$F_4(X', Y', Z') = X'^3 + 2X'Z'^2 - Y'^2Z' + 6Z'^3.$$\n",
    "\n",
    "and \n",
    "$$ E := \\{F_4(X,Y,Z) = X^3 + 2XZ^2 - Y^2Z + 6Z^3 = 0\\}$$ \n",
    "\n",
    "is in Weierstrass normal form and describes the same smooth elliptic curve as $C$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "89ac893f-3d37-446c-9713-6854103cc867",
   "metadata": {},
   "outputs": [],
   "source": [
    "T = M1*M2*M3*M4 #combined linear change of coordinates\n",
    "w = T*[X, Y, Z]\n",
    "G = F(w[1], w[2], w[3])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be4b443a-acab-4629-a595-cc12222a66d8",
   "metadata": {},
   "outputs": [],
   "source": [
    "inv(T)*[4, -5 , 2]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9096f136-916a-43d8-84e3-5540e410199b",
   "metadata": {},
   "source": [
    "If we multiply all transformation matrices $M_i$ together, we obtain \n",
    "$$A = T^{-1} = (M_1M_2M_3M_4)^{-1} = \\begin{pmatrix} 1 & 0 & -2 \\\\ -1 & -1 & 0 \\\\ 1 & 2 & 3 \\end{pmatrix}  \\in GL_3(\\mathbb{Q}).$$\n",
    "This leads to the map\n",
    "$$\\varphi\\colon \\mathbb{P}^2(\\mathbb{Q}) \\to \\mathbb{P}^2(\\mathbb{Q}) \\colon [v] \\mapsto [Av].$$\n",
    "It satisfies $\\varphi(C) = E$, where $E$ is in Weierstrass normal form\n",
    "and $\\varphi(R) = O$.\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
